#! /usr/bin/perl

# Copyright (c) 2019 Marc Espie <espie@openbsd.org>
#
# Permission to use, copy, modify, and distribute this software for any
# purpose with or without fee is hereby granted, provided that the above
# copyright notice and this permission notice appear in all copies.
#
# THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
# WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
# ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
# WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
# ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
# OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
# 
#
# look at the pics and show their size and aspect ratio

use strict;
use warnings;
use File::Find;
use FindBin;
use lib "$FindBin::Bin/../lib";
use Math::Farey;
use Data::Dumper;

my @dirs;
my @locations = (qw(pictures portrait landscape));


sub findpic
{
	my $fullname = shift;
	for my $d (@locations) {
		if ($fullname =~ m,^\Q$d\E/(.*),) {
			return $1;
		}
	}
	return undef;
}

sub rescale
{
	my ($file, $old, $new) = @_;
	my @l = ();
	open(my $in, '<', $file) or die;
	while(<$in>) {
		chomp;
		if (my ($w, $h, $x, $y, $f) = 
		    m/^\-\-trim (\d+)x(\d+)\+(\d+)\+(\d+)\s+\-\-focus\s+(.*)/) {
			my $k = findpic($f);
			if (!exists $new->{$k}) {
				print "CAN'T FIND picture $f\n";
				exit(1);
			}
			my $factor = $new->{$k}{width}/$old->{$k}{width};
			$w = int($w * $factor);
			$h = int($h * $factor);
			$x = int($x * $factor);
			$y = int($y * $factor);
			push(@l, "--trim ${w}x$h+$x+$y --focus $new->{$k}{name}\n");
		} else {
			push(@l, "$_\n");
		}
	}
	close($in);
	open(my $out, '>', $file) or die;
	print $out $_ for @l;
	close($out);
}

sub read_list
{
	open my $in, '<', "list" or return;

	my $data = {};
	while(<$in>) {
		if (my ($fullname, $width, $height) = m/^(.*)\s(\d+)x(\d+)\s/) {
			$data->{$fullname} = {name => $fullname, 
			    width => $width,
			    height => $height};
			my $k = findpic($fullname);
			if (defined $k) {
				$data->{$k} = $data->{$fullname};
			}
		} else {
			die "Incorrect line in list $_\n";
		}
	}
	return $data;
}

for my $i (@locations) {
	push(@dirs, $i) if -d $i;
}

if (@dirs == 0) {
	print STDERR "No valid directory\n";
	exit(1);
}
# first build the list of pics
my @list;
find(sub {
	return unless -f $_;
	push(@list, $File::Find::name);
    }, @dirs);

# let's get the old list
my $orig = read_list();

# then pass it off to whatever tool can do the deed
open(my $p, "-|", "gm", "identify", (sort @list)) or die;

open(my $out, ">", "list") or die;
open(my $V, ">", "portrait.list") or die;
open(my $H, ">", "landscape.list") or die;
while (<$p>) {
	chomp;
	if (m/^(.*?)(\[[0-9]+\])? JPEG ([0-9]+)x([0-9]+).*/) {
		my ($title, $h, $w) = ($1, $3, $4);
		my ($a, $b) = farey($h/ $w, 16);
		my $o;
		if ($a >= $b * 1.2) {
			$o = $H;
		} elsif ($a*1.2 <= $b) {
			$o = $V;
		} else {
			$o = $H;
		}
		print $o $title, "\n";
		print $out $title, " ", $h, "x", $w, " ", $a, ":", $b, "\n";
	} else {
		print STDERR "Bad line #$. from pipe: $_\n";
	}
}
close($out);
close($V);
close($H);
close($p);

if (defined $orig) {
	my $new = read_list();
	for my $r (glob('aspect-*')) {
		rescale($r, $orig, $new);
	}
}
